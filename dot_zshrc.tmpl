# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Docker shares safe state from host home (cli history, zoxide, ssh)
if [[ -n "$IN_DOCKER" ]] && [[ -d "/host-home" ]]; then
    if [[ -f "/host-home/.zsh_history" ]] && [[ ! -L "$HOME/.zsh_history" ]]; then
        [[ -f "$HOME/.zsh_history" ]] && rm "$HOME/.zsh_history"
        ln -sf /host-home/.zsh_history "$HOME/.zsh_history"
    fi

    if [[ -d "/host-home/.ssh" ]] && [[ ! -L "$HOME/.ssh" ]]; then
        [[ -d "$HOME/.ssh" ]] && rm -rf "$HOME/.ssh"
        ln -sf /host-home/.ssh "$HOME/.ssh"
    fi

    if [[ -d "/host-home/.cursor" ]] && [[ ! -L "$HOME/.cursor" ]]; then
        [[ -d "$HOME/.cursor" ]] && rm -rf "$HOME/.cursor"
        ln -sf /host-home/.cursor "$HOME/.cursor"
    fi

    if [[ -d "/host-home/.config/cursor" ]] && [[ ! -L "$HOME/.config/cursor" ]]; then
        mkdir -p "$HOME/.config"
        [[ -d "$HOME/.config/cursor" ]] && rm -rf "$HOME/.config/cursor"
        ln -sf /host-home/.config/cursor "$HOME/.config/cursor"
    fi

    if [[ -d "/host-home/.claude" ]] && [[ ! -L "$HOME/.claude" ]]; then
        [[ -d "$HOME/.claude" ]] && rm -rf "$HOME/.claude"
        ln -sf /host-home/.claude "$HOME/.claude"
    fi
fi

{{- if .is_multiarch_home }}
# Multi-arch home: ensure ~/.local symlink points to correct arch
# chezmoi sets this up, but check on every shell start in case arch switched
ARCH=$(uname -m)
LOCAL_ARCH_TARGET="${HOME}/.local-${ARCH}"
if [[ -L "${HOME}/.local" ]] && [[ "$(readlink "${HOME}/.local")" != "${LOCAL_ARCH_TARGET}" ]]; then
    mkdir -p "${LOCAL_ARCH_TARGET}"
    ln -sfn "${LOCAL_ARCH_TARGET}" "${HOME}/.local"
fi
{{- end }}

### Added by Zinit's installer
ZINIT_HOME="${HOME}/.local/share/zinit"
declare -A ZINIT
ZINIT[HOME_DIR]="${ZINIT_HOME}"
ZINIT[BIN_DIR]="${ZINIT_HOME}/zinit.git"

if [[ ! -f ${ZINIT[BIN_DIR]}/zinit.zsh ]]; then
    print -P "%F{33} %F{220}Installing %F{33}ZDHARMA-CONTINUUM%F{220} Initiative Plugin Manager (%F{33}zdharma-continuum/zinit%F{220})â€¦%f"
    command mkdir -p "${ZINIT_HOME}" && command chmod g-rwX "${ZINIT_HOME}"
    command git clone https://github.com/zdharma-continuum/zinit "${ZINIT[BIN_DIR]}" && \
    print -P "%F{33} %F{34}Installation successful.%f%b" || \
    print -P "%F{160} The clone has failed.%f%b"
fi

source "${ZINIT[BIN_DIR]}/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Load a few important annexes, without Turbo
# (this is currently required for annexes)
zinit light-mode for \
    zdharma-continuum/zinit-annex-as-monitor \
    zdharma-continuum/zinit-annex-bin-gem-node \
    zdharma-continuum/zinit-annex-patch-dl \
    zdharma-continuum/zinit-annex-rust

### End of Zinit's installer chunk

# Setup up some paths and environment variables

# Local paths
export PATH="${HOME}/.local/bin:${HOME}/bin:${PATH}"

# homebrew
if [ -d "/opt/homebrew" ]; then
    eval "$(/opt/homebrew/bin/brew shellenv)"
fi

# combustion directory
export COMBUSTION_DIR="${HOME}/combustion"
export MASA_HOME="${COMBUSTION_DIR}/install/MASA"

# pyenv
export PYENV_ROOT="{{ .large_install_dir }}/.local/pyenv"
if [[ -d "${PYENV_ROOT}" ]]; then
    command -v pyenv >/dev/null || export PATH="$PYENV_ROOT/bin:$PATH"
    eval "$(pyenv init -)"
fi

# exawind-manager
if [ -d "${HOME}/exawind/exawind-manager" ]; then
    export EXAWIND_MANAGER=${HOME}/exawind/exawind-manager
    source ${EXAWIND_MANAGER}/start.sh
fi

# zoxide
export _ZO_DATA_DIR=$HOME/.local/share

# hunspell dicts
if [ -d "${HOME}/.local/share/hunspell/dicts" ]; then
   export DICPATH="${HOME}/.local/share/hunspell/dicts"
fi

# ollama
export OLLAMA_NOHISTORY=true

# claude-code
if [[ -x "$(command -v claude)" ]]; then
    export CLAUDE_CODE_FILE_READ_MAX_OUTPUT_TOKENS=50000
fi

export WORKON_HOME="{{ .large_install_dir }}/.local/virtualenvs"

{{- if eq .chezmoi.os "darwin" }}

# paths for Mac OSX

# github tokens
if [ -f "${HOME}/.github_tokens" ]; then
   source "${HOME}/.github_tokens"
fi

# openmpi fails because tmp dir name is too long
# see: https://www.open-mpi.org/faq/?category=osx
export TMPDIR=/tmp

# mpi sometimes hangs
export FI_PROVIDER=tcp

# Set vlc for terminal
alias vlc="{HOME}/Applications/VLC.app/Contents/MacOS/VLC"

# Enchant lib path for python
if [[ ! -x "$(command -v pyenv)" ]]; then
   export PYENCHANT_LIBRARY_PATH=/opt/homebrew/lib/libenchant-2.dylib
fi

{{- end }}

# towards the beginning to make sure some software is loaded
zinit lucid atload'unalias zi' light-mode for "${HOME}/.local/share/zinit/plugins/hpc"

zinit ice lucid depth"1"
zinit light romkatv/powerlevel10k

zinit wait lucid light-mode \
       dl'https://raw.githubusercontent.com/ohmyzsh/ohmyzsh/master/plugins/emacs/emacsclient.sh' \
       atclone'chmod +x emacsclient.sh' \
       atpull'%atclone' \
       for "OMZP::emacs"

zinit wait lucid light-mode for \
       "OMZP::fzf" \
       has'fzf' @Aloxaf/fzf-tab \
       "OMZP::git" \
       "OMZP::poetry" \
       "OMZP::pyenv" \
       "OMZP::tmux" \
       "OMZP::zoxide"

zinit ice lucid from"gh-r" as"command" \
  mv"vivid-*/vivid -> vivid" \
  atload'export LS_COLORS="$(vivid generate one-dark)"; zstyle ":completion:*" list-colors "${(s.:.)LS_COLORS}"'
zinit light sharkdp/vivid

zinit ice from="gh-r" as="program" mv="bat* -> bat" pick="bat/bat" atload="alias cat=bat"
zinit light sharkdp/bat

zinit ice lucid as"program" pick"bin/git-dsf"
zinit load so-fancy/diff-so-fancy

# towards the end because it assumes some things are set in other plugins
zinit wait lucid light-mode for "${HOME}/.local/share/zinit/plugins/personal"

# Path to your oh-my-zsh installation.
export ZSH=$HOME/.oh-my-zsh

# OMZL options
zstyle ':omz:lib:theme-and-appearance' gnu-ls yes
ENABLE_CORRECTION="true"
CORRECT_IGNORE_FILE='.*'

# Which plugins would you like to load?
# Standard plugins can be found in ~/.oh-my-zsh/plugins/*
# Custom plugins may be added to ~/.oh-my-zsh/custom/plugins/
# Example format: plugins=(rails git textmate ruby lighthouse)
# Add wisely, as too many plugins slow down shell startup.
plugins=()

source $ZSH/oh-my-zsh.sh

# User configuration

# better terminal colors
export TERM=xterm-256color

# This is a bit sneaky... but nice!
# Separate up arrow lookback for local and global ZSH history
# http://superuser.com/questions/446594/separate-up-arrow-lookback-for-local-and-global-zsh-history/691603
# up/down arrow steps through LOCAL terminal history
# Ctrl-up/down steps through GLOBAL terminal history
# Ctrl-R searches global terminal history
bindkey "${terminfo[kpp]}" up-line-or-local-history
bindkey "${terminfo[knp]}" down-line-or-local-history

up-line-or-local-history() {
    zle set-local-history 1
    zle up-line-or-history
    zle set-local-history 0
}
zle -N up-line-or-local-history
down-line-or-local-history() {
    zle set-local-history 1
    zle down-line-or-history
    zle set-local-history 0
}
zle -N down-line-or-local-history

bindkey "^[[1;5A" up-line-or-history    # [CTRL] + Cursor up
bindkey "^[[1;5B" down-line-or-history  # [CTRL] + Cursor down

# Use ctrl-p/n to go up/down search
bindkey "^P" up-line-or-beginning-search
# bindkey "^N" down-line-or-beginning-search

# History
HISTFILE=~/.zsh_history
HISTSIZE=1000000
SAVEHIST=$HISTSIZE
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_FIND_NO_DUPS

# zsh history backups
export HIST_BACKUP_DIR="${HOME}/.zsh_history_backups"
if [ ! -d "${HIST_BACKUP_DIR}" ]; then
   mkdir "${HIST_BACKUP_DIR}"
fi

function backup_zsh_history () {
    if [ -d "${HIST_BACKUP_DIR}" ]; then
        HISTFILE_BAK="${HIST_BACKUP_DIR}/zsh_history_$(date +\%Y_\%m_\%d).bak"
        if [ ! -f "${HISTFILE_BAK}" ] && [ -f "${HISTFILE}" ]; then
            cp "${HISTFILE}" "${HISTFILE_BAK}"
        fi
    fi
}

backup_zsh_history

function backup_zoxide_database () {
    if [ -d "$_ZO_DATA_DIR" ]; then
        ZO_DATA="${_ZO_DATA_DIR}/db.zo"
        ZO_DATA_BAK="${_ZO_DATA_DIR}/db_$(date +\%Y_\%m_\%d)-bak.zo"
        if [ ! -f "${ZO_DATA_BAK}" ] && [ -f "${ZO_DATA}" ]; then
            cp "$ZO_DATA" "$ZO_DATA_BAK"
        fi
        unset ZO_DATA
        unset ZO_DATA_BAK
    fi
}

backup_zoxide_database

# Source shell secrets (prefer host secrets when in Docker)
if [[ -n "$IN_DOCKER" ]] && [[ -f "/host-home/.shell-secrets" ]]; then
    source "/host-home/.shell-secrets"
elif [[ -f "${HOME}/.shell-secrets" ]]; then
    source "${HOME}/.shell-secrets"
fi

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
